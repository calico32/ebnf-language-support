(* Grammar for Extended Backus-Naur Form (EBNF) *)

grammar = { comment | rule } ;

comment = "(*", ? any string ? - "*)", "*)" ;

rule = name "=" expression ";" ;

name = name-start-character { name-character } ;

name-start-character = letter | digit | "_" ;
name-character = name-start-character | "-" ;
letter = "a".."z" | "A".."Z" ;
digit = "0".."9" ;

expression = name | literal | special
    | group | repetition | optional
    | alternation | concatenation | range | except ;

literal = ( '"', ? any string ? - '"', '"' ) | ( "'", ? any string ? - "'", "'" ) ;
special = "?", ? any string ? - "?", "?" ;

group         = "(" expression ")" ;
repetition    = "{" expression "}" ;
optional      = "[" expression "]" ;
alternation   = expression "|" expression ;
concatenation = expression [ "," ] expression ;
range         = expression ".." expression ;
except        = expression "-" expression ;
