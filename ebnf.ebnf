(* Grammar for Extended Backus-Naur Form (EBNF) *)

grammar = { comment | rule } ;

comment = "(*", ? any string ? - "*)", "*)" ;

rule = name, "=", expression, ";" ;

name = name_start_character, { name_character } ;

name_start_character = letter | digit | "_" ;
name_character = name_start_character | "-" ;
letter = "a".."z" | "A".."Z" ;
digit = "0".."9" ;

expression = name | literal | special | group | repetition | alternation | concatenation | optional | range ;

literal = ( '"', ? any string ? - '"', '"' ) | ( "'", ? any string ? - "'", "'" ) ;
special = "?", ? any string ? - "?", "?" ;

group = "(", expression, ")" ;
repetition = "{", expression, "}" ;
alternation = expression, "|", expression ;
concatenation = expression, [ "," ], expression ;
optional = "[", expression, "]" ;
range = literal, "..", literal ;
